allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'


subprojects {
    afterEvaluate { project ->
        // override the android compileSdkVersion and namespace for each dependencies to compile with the latest Gradle
        if (project.plugins.hasPlugin("com.android.application") ||
                project.plugins.hasPlugin("com.android.library")) {
            project.android.compileSdkVersion = 35

            // override Java version to 23
            project.android.compileOptions.sourceCompatibility = JavaVersion.VERSION_23
            project.android.compileOptions.targetCompatibility = JavaVersion.VERSION_23

            // override Kotlin JVM target to 23 too
            project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = "23"
                }
            }

            if (project.android.namespace == null) {
                def manifest = new XmlSlurper().parse(file(project.android.sourceSets.main.manifest.srcFile))
                def packageName = manifest.@package.text()
                println("Setting ${packageName} as android namespace")
                project.android.namespace = packageName
            }
        }
    }
}

// override the kotlin language version for each dependencies to 2.1.10
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name == 'kotlin-gradle-plugin') {
            details.useVersion '2.1.10'
        }
    }
}

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
